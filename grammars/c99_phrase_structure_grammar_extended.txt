A.2 Phrase structure grammar
A.2.1 Expressions
(6.5.1) primary-expression:
    identifier
    constant
    string-literal
    ( expression )
(6.5.2) postfix-expression:
    primary-expression
    postfix-expression [ expression ]
    postfix-expression ( argument-expression-list opt )
    postfix-expression . identifier
    postfix-expression -> identifier
    postfix-expression ++
    postfix-expression --
    ( type-name ) { initializer-list }
    ( type-name ) { initializer-list , }
(6.5.2) argument-expression-list:
    assignment-expression
    argument-expression-list , assignment-expression
(6.5.3) unary-expression:
    postfix-expression
    ++ unary-expression
    -- unary-expression
    unary-operator cast-expression
    sizeof unary-expression
    sizeof ( type-name )
(6.5.3) unary-operator: one of
    & * + - ~ !
(6.5.4) cast-expression:
    unary-expression
    ( type-name ) cast-expression
(6.5.5) multiplicative-expression:
    cast-expression
    multiplicative-expression * cast-expression
    multiplicative-expression / cast-expression
    multiplicative-expression % cast-expression
(6.5.6) additive-expression:
    multiplicative-expression
    additive-expression + multiplicative-expression
    additive-expression - multiplicative-expression
(6.5.7) shift-expression:
    additive-expression
    shift-expression << additive-expression
    shift-expression >> additive-expression
(6.5.8) relational-expression:
    shift-expression
    relational-expression < shift-expression
    relational-expression > shift-expression
    relational-expression <= shift-expression
    relational-expression >= shift-expression
(6.5.9) equality-expression:
    relational-expression
    equality-expression == relational-expression
    equality-expression != relational-expression
(6.5.10) AND-expression:
    equality-expression
    AND-expression & equality-expression
(6.5.11) exclusive-OR-expression:
    AND-expression
    exclusive-OR-expression ^ AND-expression
(6.5.12) inclusive-OR-expression:
    exclusive-OR-expression
    inclusive-OR-expression | exclusive-OR-expression
(6.5.13) logical-AND-expression:
    inclusive-OR-expression
    logical-AND-expression && inclusive-OR-expression
(6.5.14) logical-OR-expression:
    logical-AND-expression
    logical-OR-expression || logical-AND-expression
(6.5.15) conditional-expression:
    logical-OR-expression
    logical-OR-expression ? expression : conditional-expression
(6.5.16) assignment-expression:
    conditional-expression
    unary-expression assignment-operator assignment-expression
(6.5.16) assignment-operator: one of
    = *= /= %= += -= <<= >>= &= ^= |=
(6.5.17) expression:
    assignment-expression
    expression , assignment-expression
(6.6) constant-expression:
    conditional-expression
A.2.2 Declarations
(6.7) declaration:
    declaration-specifiers init-declarator-list opt ;
(6.7) declaration-specifiers:
    storage-class-specifier declaration-specifiers opt
    type-specifier declaration-specifiers opt
    type-qualifier declaration-specifiers opt
    function-specifier declaration-specifiers opt
(6.7) init-declarator-list:
    init-declarator
    init-declarator-list , init-declarator
(6.7) init-declarator:
    declarator
    declarator = initializer
(6.7.1) storage-class-specifier:
    typedef
    extern
    static
    auto
    register
(6.7.2) type-specifier:
    void
    char
    short
    int
    long
    float
    double
    signed
    unsigned
    _Bool
    _Complex
    _Imaginary
    struct-or-union-specifier
    enum-specifier
    typedef-name
(6.7.2.1) struct-or-union-specifier:
    struct-or-union identifier opt { struct-declaration-list }
    struct-or-union identifier
(6.7.2.1) struct-or-union:
    struct
    union
(6.7.2.1) struct-declaration-list:
    struct-declaration
    struct-declaration-list struct-declaration
(6.7.2.1) struct-declaration:
    specifier-qualifier-list struct-declarator-list ;
(6.7.2.1) specifier-qualifier-list:
    type-specifier specifier-qualifier-list opt
    type-qualifier specifier-qualifier-list opt
(6.7.2.1) struct-declarator-list:
    struct-declarator
    struct-declarator-list , struct-declarator
(6.7.2.1) struct-declarator:
    declarator
    declarator opt : constant-expression
(6.7.2.2) enum-specifier:
    enum identifier opt { enumerator-list }
    enum identifier opt { enumerator-list , }
    enum identifier
(6.7.2.2) enumerator-list:
    enumerator
    enumerator-list , enumerator
(6.7.2.2) enumerator:
    enumeration-constant
    enumeration-constant = constant-expression
(6.7.3) type-qualifier:
    const
    restrict
    volatile
(6.7.4) function-specifier:
    inline
(6.7.5) declarator:
    pointer opt direct-declarator
(6.7.5) direct-declarator:
    identifier
    ( declarator )
    direct-declarator [ type-qualifier-list opt assignment-expression opt ]
    direct-declarator [ static type-qualifier-list opt assignment-expression ]
    direct-declarator [ type-qualifier-list static assignment-expression ]
    direct-declarator [ type-qualifier-list opt * ]
    direct-declarator ( parameter-type-list )
    direct-declarator ( identifier-list opt )
(6.7.5) pointer:
    * type-qualifier-list opt
    * type-qualifier-list opt pointer
(6.7.5) type-qualifier-list:
    type-qualifier
    type-qualifier-list type-qualifier
(6.7.5) parameter-type-list:
    parameter-list
    parameter-list , ...
(6.7.5) parameter-list:
    parameter-declaration
    parameter-list , parameter-declaration
(6.7.5) parameter-declaration:
    declaration-specifiers declarator
    declaration-specifiers abstract-declarator opt
(6.7.5) identifier-list:
    identifier
    identifier-list , identifier
(6.7.6) type-name:
    specifier-qualifier-list abstract-declarator opt
(6.7.6) abstract-declarator:
    pointer
    pointer opt direct-abstract-declarator
(6.7.6) direct-abstract-declarator:
    ( abstract-declarator )
    direct-abstract-declarator opt [ assignment-expression opt ]
    direct-abstract-declarator opt [ * ]
    direct-abstract-declarator opt ( parameter-type-list opt )
(6.7.7) typedef-name:
    identifier
(6.7.8) initializer:
    assignment-expression
    { initializer-list }
    { initializer-list , }
(6.7.8) initializer-list:
    designation opt initializer
    initializer-list , designation opt initializer
(6.7.8) designation:
    designator-list =
(6.7.8) designator-list:
    designator
    designator-list designator
(6.7.8) designator:
    [ constant-expression ]
    . identifier
A.2.3 Statements
(6.8) statement:
    labeled-statement
    compound-statement
    expression-statement
    selection-statement
    iteration-statement
    jump-statement
(6.8.1) labeled-statement:
    identifier : statement
    case constant-expression : statement
    default : statement
(6.8.2) compound-statement:
    { block-item-list opt }
(6.8.2) block-item-list:
    block-item
    block-item-list block-item
(6.8.2) block-item:
    declaration
    statement
(6.8.3) expression-statement:
    expression opt ;
(6.8.4) selection-statement:
    if ( expression ) statement
    if ( expression ) statement else statement
    switch ( expression ) statement
(6.8.5) iteration-statement:
    while ( expression ) statement
    do statement while ( expression ) ;
    for ( expression opt ; expression opt ; expression opt ) statement
    for ( declaration expression opt ; expression opt ) statement
(6.8.6) jump-statement:
    goto identifier ;
    continue ;
    break ;
    return expression opt ;
A.2.4 External definitions
(6.9) translation-unit:
    external-declaration
    translation-unit external-declaration
(6.9) external-declaration:
    function-definition
    declaration
(6.9.1) function-definition:
    declaration-specifiers declarator declaration-list opt compound-statement
(6.9.1) declaration-list:
    declaration
    declaration-list declaration

# add some terminal symbols so they don't get converted to string
#() enumeration-constant: 
#    1
#() string-literal:
#    2
#() constant:
#    3
#() identifier:
#    4

A.1.3 Identifiers
(6.4.2.1) identifier:
    identifier-nondigit
    identifier identifier-nondigit
    identifier digit
(6.4.2.1) identifier-nondigit:
    nondigit
    universal-character-name
#   other implementation-defined characters
(6.4.2.1) nondigit: one of
    _ a b c d e f g h i j k l m n o p q r s t u v w x y z
    A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
(6.4.2.1) digit: one of
    0 1 2 3 4 5 6 7 8 9
(6.4.3) universal-character-name:
    \\u hex-quad
    \\U hex-quad hex-quad
(6.4.3) hex-quad:
    hexadecimal-digit hexadecimal-digit
    hexadecimal-digit hexadecimal-digit
A.1.5 Constants
(6.4.4) constant:
    integer-constant
    floating-constant
    enumeration-constant
    character-constant
(6.4.4.1) integer-constant:
    decimal-constant integer-suffix opt
    octal-constant integer-suffix opt
    hexadecimal-constant integer-suffix opt
(6.4.4.1) decimal-constant:
    nonzero-digit
    decimal-constant digit
(6.4.4.1) octal-constant:
    0
    octal-constant octal-digit
(6.4.4.1) hexadecimal-constant:
    hexadecimal-prefix hexadecimal-digit
    hexadecimal-constant hexadecimal-digit
(6.4.4.1) hexadecimal-prefix: one of
    0x 0X
(6.4.4.1) nonzero-digit: one of
    1 2 3 4 5 6 7 8 9
(6.4.4.1) octal-digit: one of
    0 1 2 3 4 5 6 7
(6.4.4.1) hexadecimal-digit: one of
    0 1 2 3 4 5 6 7 8 9
    a b c d e f
    A B C D E F
(6.4.4.1) integer-suffix:
    unsigned-suffix long-suffix opt
    unsigned-suffix long-long-suffix
    long-suffix unsigned-suffix opt
    long-long-suffix unsigned-suffix opt
(6.4.4.1) unsigned-suffix: one of
    u U
(6.4.4.1) long-suffix: one of
    l L
(6.4.4.1) long-long-suffix: one of
    ll LL
(6.4.4.2) floating-constant:
    decimal-floating-constant
    hexadecimal-floating-constant
(6.4.4.2) decimal-floating-constant:
    fractional-constant exponent-part opt floating-suffix opt
    digit-sequence exponent-part floating-suffix opt
(6.4.4.2) hexadecimal-floating-constant:
    hexadecimal-prefix hexadecimal-fractional-constant binary-exponent-part floating-suffix opt
    hexadecimal-prefix hexadecimal-digit-sequence binary-exponent-part floating-suffix opt
(6.4.4.2) fractional-constant:
    digit-sequence opt . digit-sequence
    digit-sequence .
(6.4.4.2) exponent-part:
    e sign opt digit-sequence
    E sign opt digit-sequence
(6.4.4.2) sign: one of
    + -
(6.4.4.2) digit-sequence:
    digit
    digit-sequence digit
(6.4.4.2) hexadecimal-fractional-constant:
    hexadecimal-digit-sequence opt . hexadecimal-digit-sequence
    hexadecimal-digit-sequence .
(6.4.4.2) binary-exponent-part:
    p sign opt digit-sequence
    P sign opt digit-sequence
(6.4.4.2) hexadecimal-digit-sequence:
    hexadecimal-digit
    hexadecimal-digit-sequence hexadecimal-digit
(6.4.4.2) floating-suffix: one of
    f l F L
(6.4.4.3) enumeration-constant:
    identifier
(6.4.4.4) character-constant:
    ' c-char-sequence '
    L' c-char-sequence '
(6.4.4.4) c-char-sequence:
    c-char
    c-char-sequence c-char
(6.4.4.4) c-char:
    c-char-set
    escape-sequence
(6.4.4.4) c-char-set: none of
    ' \\ \n
(6.4.4.4) escape-sequence:
    simple-escape-sequence
    octal-escape-sequence
    hexadecimal-escape-sequence
    universal-character-name
(6.4.4.4) simple-escape-sequence: one of
    \\' \\" \\? \\ \\a \\b \\f \\n \\r \\t \\v
(6.4.4.4) octal-escape-sequence:
    \\ octal-digit
    \\ octal-digit octal-digit
    \\ octal-digit octal-digit octal-digit
(6.4.4.4) hexadecimal-escape-sequence:
    \\x hexadecimal-digit
    hexadecimal-escape-sequence hexadecimal-digit
A.1.6 String literals
(6.4.5) string-literal:
    " s-char-sequence opt "
    L" s-char-sequence opt "
(6.4.5) s-char-sequence:
    s-char
    s-char-sequence s-char
(6.4.5) s-char: 
    s-char-set
    escape-sequence
(6.4.5) s-char-set: none of
    " \\ \n
